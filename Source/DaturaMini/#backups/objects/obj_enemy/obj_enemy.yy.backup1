// 2021-08-14 12:46:55
#event properties (no comments/etc. here are saved)
parent_index = objA_actor;
uses_physics = false;

#event create
event_inherited();

noCount = false; //if true, not counted towards enemy kill count
global.bonus_kill[1] += 1;

arr_timers[0] = 1;

value = 0; //point value

lst_uniqueHits = ds_list_create(); //points multiplier

//hazard = true;

if(instance_exists(obj_player) && x > obj_player.x){
    
    image_xscale = -1;
    
}

fn_state_dead = function(){

	//on-enter actions
	if(cstate_new){
		
		cstate_new = false;
		
		spd_y = -5;
		state_current = method(undefined, pstate_air);
		y--;
		
		weight = max(weight, 0.5);
		
		image_angle = 0;
		
	}

    fixed = false;

	spd_x = -image_xscale * 1;
	
	deadspin += (image_xscale * 3600 * global.timeFlow) / room_speed;
	
	if(cstate_time > 0.6 || on_ground() || collision_rectangle(bbox_left + -1, bbox_top + -1, bbox_right + 1, bbox_bottom + 1, objA_solid, false, true)){
		instance_destroy();
	}

}

fn_state_bossDead = function(){

	static _s_x = 0;
	static _s_time = 0;
	
	globalvar G_id;
	
	//on-enter actions
	if(cstate_new){
		
		cstate_new = false;
		
		startNim();
		
		obj_player.cam_xTgt = -obj_player.x + x;
		obj_player.cam_yTgt = max(y, 1);
		
		_s_x = x;
		_s_time = 0;
		G_id = id;
		
		with obj_enemy{
		    
		    if(id != G_id){
		        instance_destroy();
		    }
		    
		}
		
		audio_sound_gain(obj_ui.bgmCurr, 0, 0);
		
	}

    x = _s_x + sin(current_time / 10);

    if(cstate_time > 1){
		
		with scr_place(obj_itemGlow_score, random_range(bbox_left, bbox_right), random_range(bbox_top, bbox_bottom)){
		    value = 0;
		}
		
		_s_time += TICK;
		
		if(_s_time > 0.2){
		    
		    _s_time = 0;
		    
		    audf_playSfx(sfx_explode);
		    
		}
		
	}
	
	if(cstate_time > 3){
		
		with obj_ui{
			
			fc_next = fc_clear_1;
			fc_delay = 0.5;
			
		}
		
		instance_destroy();
		
	}

}

#event destroy
event_inherited();

audf_playSfx(sfx_death);

if(!noCount){
    global.bonus_kill[0] += 1;
}

value *= 1 + (ds_list_size(lst_uniqueHits) * 0.25) + (global.hyperChain * 0.5);

while(value > 0){
	
	var _score = scr_place(obj_itemGlow_score, x, y);
	
	if(value >= 100){
		
		_score.size = 2;
		_score.value = 50;
		
	}
	
	if(value >= 500){
		
		_score.size = 3;
		_score.value = 200;
		
	}
	
	value += -_score.value;
	
}

if(!boss){

    repeat(10){
    	scr_place(obj_itemGlow_en, x, y);
    }
    
    var
    _rate = 0;
    
    if(instance_exists(obj_player)){
    	_rate = clamp(obj_player.currPly.hpRegen + -obj_player.currPly.hp, 0, 2) * 0.05;
    }
    
    if(random(1) < _rate){
    	scr_place(obj_itemGlow_hp, x, y);
    }

}

#event cleanup
event_inherited();

ds_list_destroy(lst_uniqueHits);
// 2021-03-27 13:35:25
#event properties (no comments/etc. here are saved)
parent_index = objA_actor;
uses_physics = false;

#event create
event_inherited();

arr_timers[0] = 1;

value = 0; //point value

lst_uniqueHits = ds_list_create(); //points multiplier

//hazard = true;

if(instance_exists(obj_player) && x > obj_player.x){
    
    image_xscale = -1;
    
}

fn_state_dead = function(){

	//on-enter actions
	if(cstate_new){
		
		cstate_new = false;
		
		spd_y = -5;
		state_current = method(undefined, pstate_air);
		y--;
		
		weight = max(weight, 0.5);
		
	}

	spd_x = -image_xscale * 1;
	
	deadspin += (image_xscale * 3600 * global.timeFlow) / room_speed;
	
	if(cstate_time > 0.6 || on_ground()){
		instance_destroy();
	}

}

#event destroy
event_inherited();

value *= 1 + (ds_list_size(lst_uniqueHits) * 0.25) + (global.hyperChain * 0.5);

while(value > 0){
	
	var _score = scr_place(obj_itemGlow_score, x, y);
	
	if(value >= 50){
		
		_score.size = 2;
		_score.value = 50;
		
	}
	
	if(value >= 200){
		
		_score.size = 3;
		_score.value = 200;
		
	}
	
	value += -_score.value;
	
}

repeat(10){
	scr_place(obj_itemGlow_en, x, y);
}

var
_rate = 0;

if(instance_exists(obj_player)){
	_rate = clamp(obj_player.currPly.hpRegen + -obj_player.currPly.hp, 0, 2) * 0.05;
}

if(random(1) < _rate){
	scr_place(obj_itemGlow_hp, x, y);
}

#event cleanup
event_inherited();

ds_list_destroy(lst_uniqueHits);